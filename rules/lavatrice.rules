rule "Lavatrice spina offline"
  when
    Item tuya02_Reachable changed from Online to Offline
  then
    alexa_lavatrice.sendCommand(OFF)
    tuya02_Power.postUpdate(0)
    logInfo("LAVATRICE", "Spina offline, disattivo la notifica su alexa e imposto consumo a 0W")
end

rule "Lavatrice spina spenta"
  when
    Item tuya02_Switch changed from ON to OFF
  then
    alexa_lavatrice.sendCommand(OFF)
    logInfo("LAVATRICE", "Spina spenta, disattivo la notifica su alexa")
end


rule "Lavatrice tramite consumi"
  when
    Item tuya02_Power changed
  then
    if ( alexa_lavatrice.state == ON ) {
      if ( tuya02_Power.averageSince(now.minusMinutes(2)) < 2 ) {
        val prowl = getActions("prowl", "prowl:broker:mybroker")
        prowl.pushNotification("LAVATRICE: Finita", "La lavasciuga ha finito il lavaggio")

	var String message;
	message= "La lavatrice ha finito il lavaggio. Ripeto. La lavatrice ha finito il lavaggio. Vai a sistemare i vestiti!"
	
	Echo_Soggiorno_TTS.sendCommand(message)
	Echo_Studio_TTS.sendCommand(message)
	Echo_Giochi_TTS.sendCommand(message)
	Echo_Camera_TTS.sendCommand(message)

        alexa_lavatrice.sendCommand(OFF)

        logInfo("LAVATRICE", "Lavaggio finito, mandata notifica su prowl ed Alexa, spento allarme.")
      }
    }

  if ( alexa_lavatrice.state == OFF ) {
    if ( tuya02_Power.averageSince(now.minusMinutes(2)) > 10 ) {
      logInfo("LAVATRICE", "AutoON: Consumo medio negli ultimi 2 minuti maggiore di 10W: accendo notifica Alexa!")
      alexa_lavatrice.sendCommand(ON)
    }
  }

end
