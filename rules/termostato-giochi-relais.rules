rule "Termostato Giochi"
  when
    Item spGiochi changed or
    Item sf12_Temp changed or
    Item tsGiochi changed
  then
  
  var Number cur_temp = sf12_Temp.state 
  if (tsGiochiSensor.state == 2 ) cur_temp = bs07_temperature.state
  var Number setpoint = spGiochi.state 
  var Number hysteresis = hsGiochi.state 
  var Number hsetpoint = setpoint.floatValue - hysteresis.floatValue

  if (tsGiochi.state == ON) {
      if ((cur_temp).floatValue > ((setpoint).floatValue)) {
	if (tsGiochiFlip.state == OFF && sonoff21_R2.state == ON) sendCommand(sonoff21_R2, OFF)
	if (tsGiochiFlip.state == ON && sonoff21_R2.state == OFF) sendCommand(sonoff21_R2, ON)
        if (tsGiochiLog.state == ON) logInfo("tsGiochi","tsGiochi: SPENGO - " + (cur_temp).floatValue + "C > " + (setpoint).floatValue + "C")
      } else {
      if ((cur_temp).floatValue < ((setpoint).floatValue - (hysteresis).floatValue)) {
	if (tsGiochiFlip.state == OFF && sonoff21_R2.state == OFF) sendCommand(sonoff21_R2, ON)
	if (tsGiochiFlip.state == ON  && sonoff21_R2.state == ON)  sendCommand(sonoff21_R2, OFF)
        if (tsGiochiLog.state == ON) logInfo("tsGiochi","tsGiochi: ACCENDO - " + (cur_temp).floatValue + "C < " + (hsetpoint).floatValue + "C")
      }
    }
  }
end

rule "Termostato Giochi Delta Check"
  when
    Time cron "0 0/1 * * * ?"
  then
  
  var Number cur_temp = sf12_Temp.state 
  if (tsGiochiSensor.state == 2 ) cur_temp = bs07_temperature.state
  var Number setpoint   = spGiochi.state 
  var Number hysteresis = hsGiochi.state 
  var Number hsetpoint  = setpoint.floatValue - hysteresis.floatValue
  var Number deltamax   = dmGiochi.state

  if ( cl02_metapower.state == ON && tsGiochi.state == ON && tsGiochiFix.state == ON ) {

    if ( tsGiochiFlip.state == OFF ) { // HEATING Begins

      if ( (cur_temp).floatValue > ( (setpoint).floatValue + (deltamax).floatValue ) && ( (shelly07_Power.state as Number) > 100 ) ) {
        sendCommand(tsGiochiFixTrigOFF, ON)
        if (tsGiochiDeltaLog.state == ON) logInfo("tsGiochiDelta","Heat: CYCLE relais to OFF - " + (cur_temp).floatValue + "C > " + (setpoint).floatValue + "C")
      }

      if ( (cur_temp).floatValue < ( (setpoint).floatValue - (hysteresis).floatValue - (deltamax).floatValue) && ( (shelly07_Power.state as Number) < 100 ) ) {
        sendCommand(tsGiochiFixTrigON, ON)
        if (tsGiochiDeltaLog.state == ON) logInfo("tsGiochiDelta","Heat: CYCLE relais to ON - " + (cur_temp).floatValue + "C < " + (hsetpoint).floatValue + "C")
      }

    } // HEATING Ends

  }

end

rule "Termostato Giochi Delta Fix ON"
  when
    Item tsGiochiFixTrigON received command ON
  then

    // EXIT if less than 3 minutes from start
    val last = cl02_metapower_lastChange.state as DateTimeType
    if (now.minusMinutes(3).isBefore( last.getZonedDateTime(ZoneId.systemDefault()) )) {
        logInfo("tsGiochi sonoff21_R2 stuck", "Exiting: cl02_metapower changed less than 5 minutes ago: " + cl02_metapower_lastChange.state)
        sendCommand(tsGiochiFixTrigON, OFF)
        return
    }

    sendCommand(sonoff21_R2, OFF)
    Thread::sleep(2000)
    sendCommand(sonoff21_R2, ON)

    logInfo("tsGiochiDeltaFixON","CYCLE relais to ON!")
    val prowl = getActions("prowl", "prowl:broker:mybroker")
    prowl.pushNotification("tsGiochi sonoff21_R2 stuck OFF!", "Cycling OFF and ON again, check it out!")

    sendCommand(tsGiochiFixTrigON, OFF)      

end


rule "Termostato Giochi Delta Fix OFF"
  when
    Item tsGiochiFixTrigOFF received command ON
  then

    // EXIT if less than 3 minutes from start
    val last = cl02_metapower_lastChange.state as DateTimeType
    if (now.minusMinutes(3).isBefore( last.getZonedDateTime(ZoneId.systemDefault()) )) {
        logInfo("tsGiochi sonoff21_R2 stuck", "Exiting: cl02_metapower changed less than 5 minutes ago: " + cl02_metapower_lastChange.state)
        sendCommand(tsGiochiFixTrigOFF, OFF)
        return
    }

    sendCommand(sonoff21_R2, ON)
    Thread::sleep(2000)
    sendCommand(sonoff21_R2, OFF)

    logInfo("tsGiochiDeltaFixOFF","CYCLE relais to OFF!")
    val prowl = getActions("prowl", "prowl:broker:mybroker")
    prowl.pushNotification("tsGiochi sonoff21_R2 stuck ON!", "Cycling ON and OFF again, check it out!")

    sendCommand(tsGiochiFixTrigOFF, OFF)      

end
