rule "Termostato Cameretta"
  when
    Item spCameretta changed or
    Item sf08_Temp changed or
    Item tsCameretta received command
  then
  
  var Number cur_temp = sf08_Temp.state 
  var Number setpoint = spCameretta.state 
  var Number hysteresis = hsCameretta.state 
  var Number hsetpoint = setpoint.floatValue - hysteresis.floatValue

  if (tsCameretta.state == ON) {
    if (tuya04_Switch.state == ON) {
      if ((cur_temp).floatValue > ((setpoint).floatValue)) {
        sendCommand(tuya04_Switch, OFF)
        logInfo("tsCameretta","tsCameretta: SPENGO - " + (cur_temp).floatValue + "C > " + (setpoint).floatValue + "C")
      }
    }
    else {
      if ((cur_temp).floatValue < ((setpoint).floatValue - (hysteresis).floatValue)) {
        sendCommand(tuya04_Switch, ON)
        logInfo("tsCameretta","tsCameretta: ACCENDO - " + (cur_temp).floatValue + "C < " + (hsetpoint).floatValue + "C")
      }
    }
  }
  else {
    sendCommand(tuya04_Switch, OFF)
  }
end

rule "Termostato Cameretta Auto OFF"
  when
    Item tsCameretta changed from OFF to ON
  then
    if (tsCamerettaAutoOFF.state == ON) {
      createTimer(now.plusSeconds(1800), [|
        tsCameretta.sendCommand(OFF)
        logInfo("tsCameretta","tsCameretta: Auto OFF 30 min")
      ])
    }
end
